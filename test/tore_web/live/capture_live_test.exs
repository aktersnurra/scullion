defmodule ToreWeb.CaptureLiveTest do
  use ToreWeb.ConnCase, async: false
  import Phoenix.LiveViewTest
  import Mox

  setup :verify_on_exit!
  setup :set_mox_from_context

  setup do
    {:ok, {user, _code}} = Tore.Accounts.create_admin("Gustaf")
    conn = build_conn() |> Plug.Test.init_test_session(%{user_id: user.id})
    %{conn: conn, user: user}
  end

  defp eventually(fun, attempts \\ 50)
  defp eventually(_fun, 0), do: flunk("Timed out waiting for condition")

  defp eventually(fun, attempts) do
    if fun.() do
      true
    else
      Process.sleep(10)
      eventually(fun, attempts - 1)
    end
  end

  test "mount renders empty chat", %{conn: conn} do
    {:ok, _lv, html} = live(conn, "/capture")
    assert html =~ "Fråga Tore"
  end

  test "capture renders as a sheet with a close affordance", %{conn: conn} do
    {:ok, _view, html} = live(conn, ~p"/capture")
    assert html =~ ~s(data-role="command-tray")
    assert html =~ ~s(data-role="tray-close")
  end

  test "return_to only accepts internal paths", %{conn: conn} do
    for evil <- ["https://evil.example", "//evil.example", "/\\evil.example"] do
      {:ok, _view, html} = live(conn, ~p"/capture?return_to=#{evil}")
      refute html =~ "evil.example"
    end

    {:ok, view, _html} = live(conn, ~p"/capture?return_to=/plan")

    assert view
           |> element(~s([data-role="tray-close"]))
           |> render() =~ ~s(href="/plan")
  end

  test "sending a message appends user bubble", %{conn: conn} do
    Tore.MockLLM
    |> expect(:chat_with_tools, fn _system, _messages, _tools, _opts ->
      {:ok, {:message, "Try making pasta tonight!"},
       %{prompt_tokens: 10, completion_tokens: 8, cost_usd: 0.0001}}
    end)

    {:ok, lv, _html} = live(conn, "/capture")

    html =
      lv
      |> form("form", %{message: "What should I cook?"})
      |> render_submit()

    assert html =~ "What should I cook?"

    :timer.sleep(200)
    html = render(lv)
    assert html =~ "Try making pasta tonight!"
  end

  test "Undo on a set_plan_slot bubble reverts the run and marks the bubble", %{conn: conn} do
    {:ok, recipe} =
      %Tore.Recipes.Recipe{title: "Pizza", recipe_type: :meal}
      |> Tore.Repo.insert()

    tool_call = %{
      id: "call_1",
      name: "set_plan_slot",
      args: %{"date" => "2026-06-23", "recipe_id" => recipe.id, "servings" => 4}
    }

    Tore.MockLLM
    |> expect(:chat_with_tools, 2, fn _sys, _msgs, _tools, _opts ->
      case Process.get(:turn_count, 0) do
        0 ->
          Process.put(:turn_count, 1)

          {:ok, {:tool_calls, [tool_call]},
           %{prompt_tokens: 1, completion_tokens: 1, cost_usd: Decimal.new(0)}}

        _ ->
          {:ok, {:message, "Done."},
           %{prompt_tokens: 1, completion_tokens: 1, cost_usd: Decimal.new(0)}}
      end
    end)

    {:ok, lv, _html} = live(conn, "/capture")

    lv
    |> form("form", %{message: "plan pizza tuesday"})
    |> render_submit()

    :timer.sleep(300)
    html = render(lv)
    assert html =~ "Pizza"
    assert html =~ "Undo"

    {:ok, plan_state} = Tore.Planning.load_plan("plan:2026-06-22")
    assert Map.has_key?(plan_state.slots, "tue_dinner")

    lv |> element("button", "Undo") |> render_click()

    {:ok, plan_state2} = Tore.Planning.load_plan("plan:2026-06-22")
    refute Map.has_key?(plan_state2.slots, "tue_dinner")

    html2 = render(lv)
    assert html2 =~ Gettext.dgettext(ToreWeb.Gettext, "default", "Reverted")
  end

  test "tray half state lists the origin surface's actionable predictions and tapping follows",
       %{conn: conn} do
    {:ok, note} =
      Tore.CounterNotes.create(%{
        surface: "home",
        kind: "freezer_fallback",
        title: "Frozen bolognese tonight?",
        body: "b",
        proposed_run: %{
          "kind" => "planner_command",
          "command" => "assign frozen bolognese",
          "scoped_slot" => "mon_dinner"
        }
      })

    {:ok, view, html} = live(conn, ~p"/capture?return_to=/")
    assert html =~ "Frozen bolognese tonight?"

    expect(Tore.MockLLM, :chat_with_tools, fn _s, _m, _t, _o ->
      {:ok, {:message, "done"},
       %{prompt_tokens: 1, completion_tokens: 1, cost_usd: Decimal.new(0)}}
    end)

    view
    |> element(~s(button[phx-click="follow_note"][phx-value-id="#{note.id}"]))
    |> render_click()

    eventually(fn ->
      Tore.Repo.get!(Tore.CounterNotes.CounterNote, note.id).status == "accepted" and
        not (render(view) =~ ~s(phx-click="follow_note"))
    end)
  end

  test "tray from /plan shows week predictions, not home ones", %{conn: conn} do
    {:ok, _} =
      Tore.CounterNotes.create(%{
        surface: "home",
        kind: "freezer_fallback",
        title: "Home note",
        body: "b",
        proposed_run: %{
          "kind" => "planner_command",
          "command" => "x",
          "scoped_slot" => "mon_dinner"
        }
      })

    {:ok, _view, html} = live(conn, ~p"/capture?return_to=/plan")
    refute html =~ "Home note"
  end

  test "predictions disappear once a message is sent", %{conn: conn} do
    {:ok, _} =
      Tore.CounterNotes.create(%{
        surface: "home",
        kind: "freezer_fallback",
        title: "Frozen bolognese tonight?",
        body: "b",
        proposed_run: %{
          "kind" => "planner_command",
          "command" => "assign frozen bolognese",
          "scoped_slot" => "mon_dinner"
        }
      })

    stub(Tore.MockLLM, :chat_with_tools, fn _s, _m, _t, _o ->
      {:ok, {:message, "Sure."},
       %{prompt_tokens: 1, completion_tokens: 1, cost_usd: Decimal.new(0)}}
    end)

    {:ok, view, html} = live(conn, ~p"/capture?return_to=/")
    assert html =~ "Frozen bolognese tonight?"

    html = view |> form("form", %{message: "hej"}) |> render_submit()

    refute html =~ "Frozen bolognese tonight?"

    eventually(fn -> render(view) =~ "Sure." end)
  end
end
